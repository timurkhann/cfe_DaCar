
#Область СлужубныеПроцедурыИФункции

Процедура ПрочитатьСообщение(ПараметрыОтбора) Экспорт
	
	Пока ПараметрыОтбора.Выборка.Следующий() Цикл
		
		НЗ = СоздатьНаборЗаписей();
		НЗ.Отбор.ИдентификаторСообщения.Установить(ПараметрыОтбора.ИдентификаторСообщения);
		НЗ.Отбор.Период.Установить(ПараметрыОтбора.Выборка.Период);
		НЗ.Прочитать();
		
		Для каждого СтрНЗ Из НЗ Цикл
			СтрНЗ.ДатаПрочтения = ТекущаяДатаСеанса();
			СтрНЗ.Прочитано = Истина;
			//СтрНЗ.Срочно = Ложь;
		КонецЦикла;  
		
		НЗ.Записать();
		
	КонецЦикла; 
	
КонецПроцедуры

Функция ЕстьРанееСозданныйЧат(Пользователь) Экспорт
	
	ИдентификаторСообщения = "";
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	тм_СообщенияПользователям.ИдентификаторСообщения КАК ИдентификаторСообщения
		|ИЗ
		|	РегистрСведений.тм_СообщенияПользователям КАК тм_СообщенияПользователям
		|ГДЕ
		|	тм_СообщенияПользователям.ПользовательКому = &ПользовательКому
		|
		|УПОРЯДОЧИТЬ ПО
		|	тм_СообщенияПользователям.ИдентификаторСообщения УБЫВ";
	
	Запрос.УстановитьПараметр("ПользовательКому", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ИдентификаторСообщения = Выборка.ИдентификаторСообщения;
		
	КонецЦикла;
	
	Возврат ИдентификаторСообщения;
	
КонецФункции	
 
Процедура ОтправитьСообщение(ПараметрыСообщения) Экспорт

	НЗ = СоздатьНаборЗаписей();
	НЗ.Отбор.ИдентификаторСообщения.Установить(ПараметрыСообщения.ИдентификаторСообщения);
	НЗ.Прочитать();
	
	МЗ = НЗ.Добавить();		
	ЗаполнитьЗначенияСвойств(МЗ, ПараметрыСообщения);
	
	// Файлы
	Если ПараметрыСообщения.Свойство("Файлы") 
		И ПараметрыСообщения.Файлы <> Неопределено Тогда
		ТаблицаФайлов = ПолучитьИзВременногоХранилища(ПараметрыСообщения.Файлы);
		ЗагрузитьФайлы(ПараметрыСообщения, ТаблицаФайлов);
	КонецЕсли;  

	НЗ.Записать();
	
КонецПроцедуры

Процедура ЗагрузитьФайлы(ПараметрыСообщения, ТаблицаФайлов)
	
	НачатьТранзакцию();
	Попытка
		Для каждого СтрТаблицыФайлов Из ТаблицаФайлов Цикл
			спрФайл = Справочники.тм_Файлы.СоздатьЭлемент();
			спрФайл.ИдентификаторВладелецФайла  = ПараметрыСообщения.ИдентификаторСообщения;
			спрФайл.ТипВладельца 				= Тип("РегистрСведенийЗапись.тм_СообщенияПользователям");
			спрФайл.Автор 						= Пользователи.ТекущийПользователь();
			спрФайл.Расширение             		= СтрТаблицыФайлов.Расширение;
			спрФайл.ПутьКФайлу             		= СтрТаблицыФайлов.ПутьКФайлу;
			спрФайл.Размер                 		= СтрТаблицыФайлов.Размер;
			спрФайл.ИндексКартинки        		= РаботаСФайламиСлужебныйКлиентСервер.ПолучитьИндексПиктограммыФайла(СтрТаблицыФайлов.Расширение);
			спрФайл.ИдентификаторФайла     		= СтрТаблицыФайлов.ИдентификаторФайла;
			спрФайл.Наименование				= СтрТаблицыФайлов.Имя;
			спрФайл.ДатаЗаписи					= ПараметрыСообщения.Период;
			
			ДДФайла 							= ПолучитьИзВременногоХранилища(СтрТаблицыФайлов.Адрес); 	
			спрФайл.ФайлХранилище          		= Новый ХранилищеЗначения(ДДФайла);
			
			спрФайл.Записать();
		КонецЦикла; 
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ПараметрыСообщения.ОшибкиОтправки 	= "Проблема с загрузкой файлов: " + ОписаниеОшибки();	 
	КонецПопытки; 
	
КонецПроцедуры

// Помещение в архив сообщений.
// 
// Параметры:
//  ИдентификаторСообщения - Строка
Процедура ПоместитьВАрхив(ИдентификаторСообщения) Экспорт
	
	НЗ = СоздатьНаборЗаписей();
	НЗ.Отбор.ИдентификаторСообщения.Установить(ИдентификаторСообщения);
	НЗ.Прочитать();
	
	ТЗ =  НЗ.Выгрузить();
	ТЗ.ЗаполнитьЗначения(Истина, "Архив");
	
	НЗ.Загрузить(ТЗ);
	НЗ.Записать();
					
КонецПроцедуры

#КонецОбласти
 
 