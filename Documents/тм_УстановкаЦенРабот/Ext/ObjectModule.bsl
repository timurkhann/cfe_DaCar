
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
		
		ЭтотОбъект.ДокументОснование = ДанныеЗаполнения;
		ЭтотОбъект.Ответственный = Пользователи.ТекущийПользователь();
		ЭтотОбъект.СозданАвтоматически = Истина;
		ЭтотОбъект.Дата = ДанныеЗаполнения.Дата;
		
		РаботыЗаказНаряда = ДанныеЗаполнения.Работы;

		Для каждого Стр Из РаботыЗаказНаряда Цикл
			
			Если Не Стр.Номенклатура.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Работа Тогда
				ЭтотОбъект.Комментарий = "Номенклатура " + Стр.Номенклатура + " не добавлена в ТЧ работы, т.к. тип номенклатуры " + Строка(Стр.Номенклатура.ТипНоменклатуры);
				Продолжить;
			КонецЕсли; 
			
			СтрокаРабот = ЭтотОбъект.Работы.Добавить();	
			СтрокаРабот.Автомобиль 		 = ДанныеЗаполнения.тм_Автомобиль.Автомобиль;
			СтрокаРабот.МодельКузова 	 = ДанныеЗаполнения.тм_Автомобиль.МодельКузова;
			СтрокаРабот.Работа 	  		 = Стр.Номенклатура;
			СтрокаРабот.СторонаУстановки = Стр.тм_СторонаУстановки;
			СтрокаРабот.Цена       		 = Стр.Цена;
			СтрокаРабот.ГодВыпуска       = ДанныеЗаполнения.тм_Автомобиль.ГодВыпуска;
			
		КонецЦикла;  
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 

	СформироватьДвиженияВРегистры();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвиженияВРегистры()

	// регистр тм_ЦеныРаботАвтомобилей
	Движения.тм_ЦеныРаботАвтомобилей.Записывать = Истина;
	Для Каждого ТекСтрокаРаботы Из Работы Цикл
		Движение = Движения.тм_ЦеныРаботАвтомобилей.Добавить();
		Движение.Период 			= Дата;
		Движение.Работа 			= ТекСтрокаРаботы.Работа;
		Движение.СторонаУстановки 	= ТекСтрокаРаботы.СторонаУстановки;
		Движение.Автомобиль 		= ТекСтрокаРаботы.Автомобиль;
		Движение.МодельКузова 		= ТекСтрокаРаботы.МодельКузова;
		Движение.Ответственный 		= Ответственный;
		Движение.ГодВыпуска 		= ТекСтрокаРаботы.ГодВыпуска;
		Движение.Цена 				= ТекСтрокаРаботы.Цена;
	КонецЦикла;

КонецПроцедуры
 
#КонецОбласти 