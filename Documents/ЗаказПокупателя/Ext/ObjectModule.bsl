
#Область ОбработчикиСобытий

&После("ПередЗаписью")
Процедура тм_ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	                                                       
	Права = тм_КЭШ.ПолучитьПраваОбъекта("ЗаказПокупателя", Пользователи.ТекущийПользователь());	
	
	Если (РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения) И (Не Права.SA) Тогда
		Если Ссылка.СостояниеЗаказа = Справочники.СостоянияЗаказНарядов.Завершен Тогда
			Сообщить("Документ в статусе завершен. Отмена невозможна!");
			Отказ = Истина;
			Возврат;
		КонецЕсли; 	
	КонецЕсли; 
		
	Если Ссылка.Проведен И СостояниеЗаказа = Справочники.СостоянияЗаказНарядов.Завершен Тогда
		ДополнительныеСвойства.Вставить("Перепроведение", Истина);
	КонецЕсли; 
	 	
	ПроверитьПоследнееСостояниеДокумента();
	
КонецПроцедуры
 
&После("ПриКопировании")
Процедура тм_ПриКопировании(ОбъектКопирования)
	
	Если Не ЭтотОбъект.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
		Возврат;
	КонецЕсли; 
	
	ЭтотОбъект.тм_ИспользованныеБонусы = 0;
	ЭтотОбъект.тм_Пробег = 0;
	
	тм_ТекущийПользователь = Пользователи.ТекущийПользователь();
	Для каждого СтрЗапасы Из ЭтотОбъект.Запасы Цикл
		
		СтрЗапасы.тм_Сотрудник = тм_КЭШ.СотрудникПользователя(тм_ТекущийПользователь);
		
	КонецЦикла;  
	
КонецПроцедуры

&После("ОбработкаЗаполнения")
Процедура тм_ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		//И Не ДанныеЗаполнения.Свойство("ВидОперации") Тогда  // Не ДанныеЗаполнения.Свойство("ВидОперации") - исключаем заказ-наряды		
		ЭтотОбъект.Ответственный = тм_КЭШ.СотрудникПользователя(Пользователи.ТекущийПользователь());		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПоследнееСостояниеДокумента()

	ДокументВСостоянииЗавершен = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 	"ВЫБРАТЬ
	|	тм_ЗаказНаряды.СостояниеЗаказа КАК СостояниеЗаказа
	|ИЗ
	|	РегистрНакопления.тм_ЗаказНаряды КАК тм_ЗаказНаряды
	|ГДЕ
	|	тм_ЗаказНаряды.Регистратор = &Заказ
	|	И тм_ЗаказНаряды.СостояниеЗаказа = &СостояниеЗаказа";
	
	Запрос.УстановитьПараметр("Заказ", Ссылка);
	Запрос.УстановитьПараметр("СостояниеЗаказа", Справочники.СостоянияЗаказНарядов.Завершен);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ДокументВСостоянииЗавершен = Истина;
	КонецЕсли;

	ДополнительныеСвойства.Вставить("ДокументВСостоянииЗавершен", ДокументВСостоянииЗавершен);

КонецПроцедуры

#КонецОбласти 