
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура тм_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ДобавитьВставитьЭлементыНаФорму();	

КонецПроцедуры

&НаКлиенте
Процедура тм_ПриОткрытииПосле(Отказ)
	
	ОтключитьВозможностьПроведениеДокументов();	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСостояниеРасходнойНакладной(Элемент)
	
	Если ПустаяСтрока(Элемент.ТекстРедактирования) Тогда
		ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбораДинамическогоСписка(Список, "СостояниеРасходнойНакладной");  	
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "СостояниеРасходнойНакладной", ЭтотОбъект.тм_ОтборСостояниеРасходнойНакладной, ВидСравненияКомпоновкиДанных.Равно,, Истина); 		
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОтключитьВозможностьПроведениеДокументов()
	
	//Отключаем из выбора "Контекстное меню"
	СписокКонтекстноеМенюПроведение = Элементы.Список.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("СписокКонтекстноеМенюПроведение");
	СписокКонтекстноеМенюПроведение.Доступность = Ложь;
	Если Не СписокКонтекстноеМенюПроведение = Неопределено Тогда
		Для каждого Элемент Из СписокКонтекстноеМенюПроведение.ПодчиненныеЭлементы Цикл
			Элемент.Доступность = Ложь;
		КонецЦикла;  
	КонецЕсли; 
	
	//Отключаем из выбора "Еще"
	ГруппаКоманднаяПанельНакладные = Элементы.Найти("ГруппаКоманднаяПанельНакладные");
	Если Не ГруппаКоманднаяПанельНакладные = Неопределено Тогда
		ФормаПроведение = ГруппаКоманднаяПанельНакладные.ПодчиненныеЭлементы.Найти("ФормаПроведение");
		Если Не ФормаПроведение = Неопределено Тогда
			Для каждого Элемент Из ФормаПроведение.ПодчиненныеЭлементы Цикл
				Элемент.Доступность = Ложь;
			КонецЦикла;  
		КонецЕсли;
		//Отключаем из выбора "Еще"пометку удаления
		ФормаУстановитьПометкуУдаления = ГруппаКоманднаяПанельНакладные.ПодчиненныеЭлементы.Найти("ФормаУстановитьПометкуУдаления");
		Если Не ФормаУстановитьПометкуУдаления = Неопределено Тогда
			ФормаУстановитьПометкуУдаления.Доступность = Ложь; 
		КонецЕсли; 
	КонецЕсли; 

КонецПроцедуры // ОтключитьВозможностьПроведениеДокументов()

&НаСервере
Процедура ДобавитьВставитьЭлементыНаФорму()
		
	Список.ТекстЗапроса = тм_ПереопределяемыйСервер.ПереопределитьТекстЗапросаДинамическогоСписка("ФормаСпискаРасходнаяНакладная", Список.ТекстЗапроса);
	
	// тм_СостояниеРасходнойНакладной
	ЭлементСостояниеРН = тм_ОбщегоНазначенияВызовСервера.ДобавитьЭлементНаФорму(
		ЭтаФорма,
		"тм_СостояниеРасходнойНакладной",
		Тип("ПолеФормы"),
		ВидПоляФормы.ПолеВвода,
		Элементы.Список,
		"Список.СостояниеРасходнойНакладной");   
		
	ЭлементСостояниеРН.Заголовок = "Состояние";
	
	// Номер телефона
	ЭлементНомерТелефона = тм_ОбщегоНазначенияВызовСервера.ДобавитьЭлементНаФорму(
		ЭтаФорма,
		"тм_НомерТелефона",
		Тип("ПолеФормы"),
		ВидПоляФормы.ПолеВвода,
		Элементы.Список,
		"Список.НомерТелефона");   
		
	ЭлементНомерТелефона.Заголовок = "Номер телефона";

		
	ДобавляемыеРеквизиты = Новый Массив;                   
		
	//Реквизит_тм_ОтборАвтомобиль = Новый РеквизитФормы("тм_ОтборАвтомобиль", Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)));
	Реквизит_тм_ОтборСостояниеРасходнойНакладной = Новый РеквизитФормы("тм_ОтборСостояниеРасходнойНакладной", Новый ОписаниеТипов("ПеречислениеСсылка.тм_СостояниеРасходнойНакладной"));
	
	ДобавляемыеРеквизиты.Добавить(Реквизит_тм_ОтборСостояниеРасходнойНакладной);
	
	ИзменитьРеквизиты(ДобавляемыеРеквизиты);		
		
	ЭлементОтборСостояниеРасходнойНакладной = тм_ОбщегоНазначенияВызовСервера.ВставитьЭлементНаФорму(
		ЭтаФорма,
		"тм_ОтборСостояниеРасходнойНакладной",
		Тип("ПолеФормы"),
		ВидПоляФормы.ПолеВвода,
		Элементы.ФильтрыНастройкиИДопИнфо,
		"тм_ОтборСостояниеРасходнойНакладной",
		Элементы.ДекорацияПустойОтступ);
		
	ЭлементОтборСостояниеРасходнойНакладной.ПодсказкаВвода = "Состояние";
	ЭлементОтборСостояниеРасходнойНакладной.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементОтборСостояниеРасходнойНакладной.КнопкаОткрытия = Ложь;
	ЭлементОтборСостояниеРасходнойНакладной.КнопкаОчистки  = Истина;
	
	ЭлементОтборСостояниеРасходнойНакладной.УстановитьДействие("ПриИзменении", "ОтборСостояниеРасходнойНакладной");
	
КонецПроцедуры // ДобавитьВставитьЭлементыНаФорму()

#КонецОбласти 