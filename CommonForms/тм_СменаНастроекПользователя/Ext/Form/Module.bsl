

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ДоступНаСменуРазрешен() Тогда
		ЗаполнитьТекущиеНастройки();	
	Иначе
		Отказ = Истина;	
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	
	СохранитьНастройкиНаСервере();
	Закрыть();

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьТекущиеНастройки()

	НастройкаКасса  = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКасса;
	НастройкаСклад	= ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойСклад;
	Пользователь	= ПараметрыСеанса.ТекущийПользователь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НастройкиПользователей.Настройка КАК Настройка,
		|	НастройкиПользователей.Значение КАК Значение,
		|	""Склад"" КАК ИмяНастройки
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
		|ГДЕ
		|	НастройкиПользователей.Пользователь = &Пользователь
		|	И НастройкиПользователей.Настройка = &НастройкаСклад
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	НастройкиПользователей.Настройка,
		|	НастройкиПользователей.Значение,
		|	""Касса""
		|ИЗ
		|	РегистрСведений.НастройкиПользователей КАК НастройкиПользователей
		|ГДЕ
		|	НастройкиПользователей.Пользователь = &Пользователь
		|	И НастройкиПользователей.Настройка = &НастройкаКасса";
	
	Запрос.УстановитьПараметр("НастройкаКасса", НастройкаКасса);
	Запрос.УстановитьПараметр("НастройкаСклад", НастройкаСклад);
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ИмяНастройки = "Склад" Тогда
			ОсновнойСкладТекущий = Выборка.Значение;	
		ИначеЕсли Выборка.ИмяНастройки = "Касса" Тогда
			ОсновнаяКассаТекущая = Выборка.Значение;
		КонецЕсли; 
	КонецЦикла;	

КонецПроцедуры // ЗаполнитьТекущиеНастройки()

&НаСервере
Процедура СохранитьНастройкиНаСервере()
	
	НастройкаКасса  = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнаяКасса;
	НастройкаСклад	= ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойСклад;
	Пользователь	= ПараметрыСеанса.ТекущийПользователь;

	Если ЗначениеЗаполнено(ОсновнаяКасса) Тогда
		МЗ = РегистрыСведений.НастройкиПользователей.СоздатьМенеджерЗаписи();
		МЗ.Пользователь = Пользователь;
		МЗ.Настройка 	= НастройкаКасса;
		МЗ.Значение	    = ОсновнаяКасса;
		МЗ.Записать();
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ОсновнойСклад) Тогда
		МЗ = РегистрыСведений.НастройкиПользователей.СоздатьМенеджерЗаписи();
		МЗ.Пользователь = Пользователь;
		МЗ.Настройка 	= НастройкаСклад;
		МЗ.Значение	    = ОсновнойСклад;
		МЗ.Записать();
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ДоступНаСменуРазрешен()

	Возврат тм_ОбщегоНазначенияВызовСервера.НастройкиПользователей("Показывать смену настроек", ПараметрыСеанса.ТекущийПользователь, Ложь);
	
КонецФункции // ДоступНаСменуРазрешен()
 
#КонецОбласти 
